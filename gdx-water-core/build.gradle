import java.sql.DriverManager

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.srjohnathan'
version '1.0-SNAPSHOT'
def  isReleaseVersion = !version.endsWith("SNAPSHOT")



repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }

}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation "com.badlogicgames.gdx:gdx:1.10.0"

}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java


            pom {
                name = project.name
                description = "Dedicated library for using Effekseer particle tools in libGDX"
                url = "https://github.com/SrJohnathan/gdx-effekseer"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "johnathan.stark42"
                        name = "Antonio Johnathan"
                        email = "johnathan.stark42@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git@github.com:SrJohnathan/gdx-effekseer.git"
                    developerConnection = "scm:git@github.com:SrJohnathan/gdx-effekseer.git"
                    url = "https://github.com/SrJohnathan/gdx-effekseer"
                }
            }

        }
    }
    repositories {
        maven {
            name = project.name

            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo

            //url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("MAVEN_USERNAME")
                password = findProperty("MAVEN_PASSWORD")
            }
        }
    }
}



signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}



def coreWrapperDir=new File("${projectDir}/src/main/java/io/github/srjohnathan/gdx/water/fftw")
task createCoreWrapperDir{
    coreWrapperDir.mkdirs()
}

task runSwig(type:Exec, dependsOn:['createCoreWrapperDir']){
    commandLine 'swig'
    args '-c++', '-java', '-package', 'io.github.srjohnathan.gdx.water.fftw', '-outdir', coreWrapperDir.absolutePath, '-o',"${rootProject.projectDir}/cpp/dll.cpp", "${rootProject.projectDir}/jni/st.i"
}

